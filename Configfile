LANGUAGES   += c++
COMPILEOPTS += -Wall
COMPILEOPTS += -Wextra
COMPILEOPTS += -Werror
COMPILEOPTS += -pedantic
COMPILEOPTS += -std=c++0x

# This is the only way I know how to make C++ use internal libraries,
# unfortunately...
LANGUAGES   += c++
COMPILEOPTS += -Isrc

# Everything here actually depends on libflo, as that's the graph
# format we're using (even when outputing Chisel code).
LANGUAGES   += c++
COMPILEOPTS += `ppkg-config flo --cflags`
LINKOPTS    += `ppkg-config flo --libs`

# GMP doesn't have a pkg-config file, which IMHO is a bug!
LANGUAGES   += c++
LINKOPTS    += -lgmp
LINKOPTS    += -lgmpxx

# The test scripts use BASH
LANGUAGES   += bash

# A Chisel (and Flo) torture tester.  This mixes together different
# Chisel patterns and produces a large Chisel graph.
BINARIES    += chisel-torture
SOURCES     += torture.c++
COMPILEOPTS += -DCHISEL
CONFIG      += auto_patterns
CONFIG      += auto_seed

BINARIES    += flo-torture
SOURCES     += torture.c++
COMPILEOPTS += -DFLO
CONFIG      += auto_patterns
CONFIG      += auto_seed

# Allows for examination of single patterns.  There's two versions of
# this: one that outputs to Flo files and out that outputs to Chisel
# files.
BINARIES    += chisel-patterns
SOURCES     += patterns.c++
COMPILEOPTS += -DCHISEL
CONFIG      += auto_patterns
CONFIG      += auto_test_patterns

BINARIES    += flo-patterns
SOURCES     += patterns.c++
COMPILEOPTS += -DFLO
CONFIG      += auto_patterns
TESTSRC     += all_patterns.bash
CONFIG      += auto_test_patterns
